
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

atom = @{ UPPERCASE_LETTER ~ (LETTER | NUMBER | "_" | "'")* }
var = @{ LOWERCASE_LETTER ~ (LETTER | NUMBER | "_" | "'")* }
bottom = @{ "_|_" }

iexp = { aexp ~ ( "->" ~ iexp )?  }
aexp = { oexp ~ ( "/\\" ~ aexp )?  }
oexp = { app ~ ( "\\/" ~ oexp )?  }
app = { atomic+ }
atomic = { atom | var | bottom | ( "(" ~ iexp ~ ")" ) }

type_ = { SOI ~ iexp ~ EOI }

data = {
  SOI
  ~ atom
  ~ vars
  ~ ("=" ~ constrs)?
  ~ EOI
}

vars = { var* }

constrs = { constr ~ ("|" ~ constr)* }

constr = { atom ~ atomic* }
